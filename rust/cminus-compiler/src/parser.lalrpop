grammar;

use std::i32;
use ast::{Meta, Program, Parameter, Expr, Stmt, Operator, push_to_vec};

pub Program: Program = <ExternalDeclaration*>;

pub ExternalDeclaration: Meta<Stmt> = {
    <l:@L> "int" <i: Identifier> "(" <ps: ParameterList> ")" <b: Block> => Meta::new(Stmt::Function(i, ps, Box::new(b)), l),
    <Declaration>,
};

pub Declaration: Meta<Stmt> = {
    <l:@L> "int" <is: DeclIdentifierList> ";" => Meta::new(Stmt::Declaration(is), l),
}

pub Subs: Vec<i32> = {
    "[" <Constant> "]" => vec![<>],
    <s: Subs> "[" <c: Constant> "]" => push_to_vec(s, c),
}

pub PSubs: Vec<i32> = {
    "[" "]" => vec![-1],
    <p: PSubs> "[" "]" => push_to_vec(p, -1),
}

pub DeclIdentifierList: Vec<Parameter> = {
    <d: DeclIdentifier> => vec![d],
    <ds: DeclIdentifierList> "," <d: DeclIdentifier> => push_to_vec(ds, d),
    () => vec![],
}

pub DeclIdentifier: Parameter = {
    <i: Identifier> => Parameter { identifier: i, sub_arrays: vec![] },
    <i: Identifier> <s: Subs> => Parameter { identifier: i, sub_arrays: s },
}

pub ParameterList: Vec<Parameter> = {
    () => vec![],
    <ps: ParameterList> "," <p: Parameter> => push_to_vec(ps, p),
    <p: Parameter> => vec![p],
}

pub ExprList: Vec<Meta<Expr>> = {
    () => vec![],
    <e: Expr> => vec![e],
    <es: ExprList> "," <e: Expr> => push_to_vec(es, e),
}

pub Parameter: Parameter = {
    "int" <i: Identifier> => Parameter { identifier: i, sub_arrays: vec![] },
    "int" <s: PSubs> <i: Identifier> => Parameter { identifier: i, sub_arrays: s },
    "int" <i: Identifier> <s: PSubs> => Parameter { identifier: i, sub_arrays: s },
}

pub Block: Meta<Stmt> = {
    <l:@L> "{" <sl: Statement*> "}" => Meta::new(Stmt::Block(sl), l),
}

pub FunctionCall: Meta<Expr> = {
    <l:@L> <f: Identifier> "(" <es: ExprList> ")" => Meta::new(Expr::FunctionCall(f, es), l),
}

pub Statement: Meta<Stmt> = {
    <Block>,
    <Declaration>,
    <l:@L> "if" "(" <e: Expr> ")" <s: Statement> => Meta::new(Stmt::If(e, Box::new(s)), l),
    <l:@L> "while" "(" <e: Expr> ")" <s: Statement> => Meta::new(Stmt::While(e, Box::new(s)), l),
    <l:@L> "for" "(" <e1: Expr> ";" <e2: Expr> ";" <e3: Expr> ")" <s: Statement> => Meta::new(Stmt::For(e1, e2, e3, Box::new(s)), l),
    <l:@L> "return" <e: Expr> ";" => Meta::new(Stmt::Return(e), l),
    <l:@L> "read" "(" <i: Identifier> ")" ";" => Meta::new(Stmt::Read(i), l),
    <l:@L> "write" "(" <e: Expr> ")" ";" => Meta::new(Stmt::Write(e), l),
    <l:@L> <e: Expr> ";" => Meta::new(Stmt::Expr(e), l),
    <l:@L> <f: FunctionCall> ";" => Meta::new(Stmt::Expr(f), l),
}

pub Expr: Meta<Expr> = {
    <AssignmentExpr>,
}

pub AssignmentExpr: Meta<Expr> = {
    <l:@L> <i: UnaryExpr> "=" <e: LogicalOrExpr> => Meta::new(Expr::Assignment(Box::new(i), Box::new(e)), l),
    <l:@L> <i: UnaryExpr> "=" <f: FunctionCall> => Meta::new(Expr::Assignment(Box::new(i), Box::new(f)), l),
    <LogicalOrExpr>,
}

pub LogicalOrExpr: Meta<Expr> = {
    <l:@L> <e1: LogicalAndExpr> "||" <e2: LogicalOrExpr> => Meta::new(Expr::Binary(Operator::LogicalOr, Box::new(e1), Box::new(e2)), l),
    <LogicalAndExpr>,
}

pub LogicalAndExpr: Meta<Expr> = {
    <l:@L> <e1: OrExpr> "&&" <e2: LogicalAndExpr> => Meta::new(Expr::Binary(Operator::LogicalAnd, Box::new(e1), Box::new(e2)), l),
    <OrExpr>,
}

pub OrExpr: Meta<Expr> = {
    <l:@L> <e1: XorExpr> "|" <e2: OrExpr> => Meta::new(Expr::Binary(Operator::Or, Box::new(e1), Box::new(e2)), l),
    <XorExpr>,
}

pub XorExpr: Meta<Expr> = {
    <l:@L> <e1: AndExpr> "^" <e2: XorExpr> => Meta::new(Expr::Binary(Operator::Xor, Box::new(e1), Box::new(e2)), l),
    <AndExpr>,
}

pub AndExpr: Meta<Expr> = {
    <l:@L> <e1: EqExpr> "&" <e2: AndExpr> => Meta::new(Expr::Binary(Operator::And, Box::new(e1), Box::new(e2)), l),
    <EqExpr>,
}

pub EqExpr: Meta<Expr> = {
    <l:@L> <e1: RelExpr> "==" <e2: EqExpr> => Meta::new(Expr::Binary(Operator::Equal, Box::new(e1), Box::new(e2)), l),
    <l:@L> <e1: RelExpr> "!=" <e2: EqExpr> => Meta::new(Expr::Binary(Operator::NotEqual, Box::new(e1), Box::new(e2)), l),
    <RelExpr>,
}

pub RelExpr: Meta<Expr> = {
    <l:@L> <e1: AddExpr> ">" <e2: RelExpr> => Meta::new(Expr::Binary(Operator::Greater, Box::new(e1), Box::new(e2)), l),
    <l:@L> <e1: AddExpr> ">=" <e2: RelExpr> => Meta::new(Expr::Binary(Operator::GreaterEqual, Box::new(e1), Box::new(e2)), l),
    <l:@L> <e1: AddExpr> "<" <e2: RelExpr> => Meta::new(Expr::Binary(Operator::Less, Box::new(e1), Box::new(e2)), l),
    <l:@L> <e1: AddExpr> "<=" <e2: RelExpr> => Meta::new(Expr::Binary(Operator::LessEqual, Box::new(e1), Box::new(e2)), l),
    <AddExpr>,
}

pub AddExpr: Meta<Expr> = {
    <l:@L> <e1: MulExpr> "+" <e2: AddExpr> => Meta::new(Expr::Binary(Operator::Add, Box::new(e1), Box::new(e2)), l),
    <l:@L> <e1: MulExpr> "-" <e2: AddExpr> => Meta::new(Expr::Binary(Operator::Subtract, Box::new(e1), Box::new(e2)), l),
    <MulExpr>,
}

pub MulExpr: Meta<Expr> = {
    <l:@L> <e1: UnaryExpr> "*" <e2: MulExpr> => Meta::new(Expr::Binary(Operator::Multiply, Box::new(e1), Box::new(e2)), l),
    <l:@L> <e1: UnaryExpr> "/" <e2: MulExpr> => Meta::new(Expr::Binary(Operator::Divide, Box::new(e1), Box::new(e2)), l),
    <l:@L> <e1: UnaryExpr> "%" <e2: MulExpr> => Meta::new(Expr::Binary(Operator::Mod, Box::new(e1), Box::new(e2)), l),
    <UnaryExpr>,
}

pub UnaryExpr: Meta<Expr> = {
    <l:@L> "--" <e1: PostfixExpr> => Meta::new(Expr::Unary(Operator::PreDecr, Box::new(e1)), l),
    <l:@L> "++" <e1: PostfixExpr> => Meta::new(Expr::Unary(Operator::PreDecr, Box::new(e1)), l),
    <l:@L> "&" <e1: PostfixExpr> => Meta::new(Expr::Unary(Operator::Ref, Box::new(e1)), l),
    <l:@L> "*" <e1: PostfixExpr> => Meta::new(Expr::Unary(Operator::Deref, Box::new(e1)), l),
    <l:@L> "-" <e1: PostfixExpr> => Meta::new(Expr::Unary(Operator::Negate, Box::new(e1)), l),
    <l:@L> "+" <e1: PostfixExpr> => Meta::new(Expr::Unary(Operator::Positive, Box::new(e1)), l),
    <l:@L> "!" <e1: PostfixExpr> => Meta::new(Expr::Unary(Operator::Not, Box::new(e1)), l),
    <PostfixExpr>,
}

pub PostfixExpr: Meta<Expr> = {
    <l:@L> <e1: PostfixExpr> "[" <e2: Expr> "]" => Meta::new(Expr::Binary(Operator::ArrayAccess, Box::new(e1), Box::new(e2)), l),
    <l:@L> <e1: PostfixExpr> "--" => Meta::new(Expr::Unary(Operator::PostDecr, Box::new(e1)), l),
    <l:@L> <e1: PostfixExpr> "++" => Meta::new(Expr::Unary(Operator::PostIncr, Box::new(e1)), l),
    <RootExpr>,
}

pub RootExpr: Meta<Expr> = {
    <l:@L> <c: Constant> => Meta::new(Expr::Number(c), l),
    <l:@L> <s: r#""[^"]*""#> => Meta::new(Expr::Str(s[1..s.len()-1].to_string()), l),
    <l:@L> <i: Identifier> => Meta::new(Expr::Identifier(i), l),
    "(" <Expr> ")" => <>,
}

pub Constant: i32 = {
    "0" => 0,
    r"[1-9][0-9]*" => <>.parse().unwrap(),
    <s: r"0[0-9]+"> => i32::from_str_radix(&s[1..s.len()-1], 8).unwrap(),
    <s: r"0x[1-9][0-9]*"> => i32::from_str_radix(&s[2..s.len()-1], 16).unwrap(),
}

pub Identifier: String = {
    r"[a-zA-Z][a-zA-Z0-9_]*" => <>.to_string(),
}
